- # 从1+2+3+4+...+100

  

  ## 栈

  

  ```
  ;从1加到100
  s: ;开始标号
        jmp start
  message db '1+2+3+...+100=' ;字符串等同于db '1','+','2'...
  ;message代表这一行的汇编地址
  ; 传送准备阶段
  ; ds:si 原始数据串的段地址:偏移地址
  ; es:di 目标位置的段地址:偏移地址
  
  start:
        mov ax,0x7c0 ;设置数据段的段基地址
        mov ds,ax
  
        mov ax,0xb800 ;设置附加段的段基地址到显示缓冲区
        mov es,ax
  
        ;显示字符
        mov si,message
        mov di,0
        mov cx,start - message ;循环次数
  showmsg:
        mov al,[si] ;将si对应的数据(字符)传送到al寄存器
        mov [es:di],al ;将al寄存器的值，传送给显存缓冲区
        inc di ;di偏移地址+1向后面移动一个字节
        mov byte [es:di],0x07 ;为字符设置显示属性颜色等数据
        inc di ;di偏移地址+1向后面移动一个字节，为下一个字节空间放置字符做准备
        inc si ;si +1获取下一个字符
        loop showmsg
  
        ;计算1到100的和
        xor ax,ax ;ax用于存储累加结果，异或指令清零
        mov cx,1
  summate:
        add ax,cx
        inc cx
        cmp cx,100 ;cx与100进行比较
        jle summate ;如果小于等于则跳转
  
        ;mov ax, number
        ;分解各个数位
        mov bx,number
        mov cx,5 ;循环次数
        mov si,10 ;除数
  
  digit:
        xor dx,dx ;重置dx
        div si
        mov [bx], dl ;保留数位（余数）5050 => 0505
        inc bx
        loop digit
  
        ;开始展示各个数位
        mov bx, number ;将存有各个数位的首地址存储到bx 基址寄存器
        mov si, 4; 0,0,0,0,0 -> 4 整体是0-4 想要从后往前
  show:
       ;这里si被称为Index Register索引寄存器或者变址寄存器，另一个变址寄存器是di
        mov al, [bx+si];bx不变，通过si变化访问地址数据
        ;bx存放的是基地址，是标号number后面，第一个数位的偏移地址
        ;加上si的索引值之后，就可以访问其他数位的偏移地址
        add al, 0x30 ;行成字符编码
        mov ah, 0x04 ;字符颜色属性
        mov [es:di], ax
        add di, 2
        dec si ;递减，指向前一个数位
        ;jns条件转移指令 处理器执行它时，要参考标志寄存器的sf为，即标志位
        jns show ;jns条件转移指令 如果标志寄存器（flags）的标志位符号为不是1，
        ;则跳转到标号show处执行
        ;当si的值一值-1结果为-1时，标志寄存器（flags）的标志位置1
        ;此时不满足jns指令要求，会向下执行语句
  
        jmp $
  
  number db 0,0,0,0,0
  
  e: ;结束标号
         times 510 - (e - s) db 0
         db 0x55, 0xaa
  ```

  ## 将数位存在栈中

  

  ![image.png](https://cdn.nlark.com/yuque/0/2021/png/288075/1620050584459-c4eef5a8-4a67-41ce-b2d1-5938853a803e.png)

  特殊读写方式

  ![image.png](https://cdn.nlark.com/yuque/0/2021/png/288075/1620050652618-ef820f53-f82c-43ae-9bf2-b491ecf5542b.png)

  push 入栈 压栈

  pop 出栈 弹栈

  ![image.png](https://cdn.nlark.com/yuque/0/2021/png/288075/1620050848820-92feb6b8-e75d-42ae-93a0-3fd5fdf2c768.png)

  压栈 出栈细节不用关心

  下一个压栈位置 和出栈位置在SP栈偏移地址；SS栈段的段地址

  

  栈段寄存器SS左移4位，加上SP栈偏移地址就得到了访问内存所需的20位物理地址

  

  栈段也是内存的一部分

  

  ![image.png](https://cdn.nlark.com/yuque/0/2021/png/288075/1620051274613-0adc6914-7079-4fb8-94ae-636eee85d55e.png)

  

  ![image.png](https://cdn.nlark.com/yuque/0/2021/png/288075/1620051460907-6e1f62a8-3709-4ad7-8354-c41d3fd0f9c2.png)

  ![image.png](https://cdn.nlark.com/yuque/0/2021/png/288075/1620051871451-a8e2cec0-b63f-4210-b56b-b1bb1464595b.png)

  ![image.png](https://cdn.nlark.com/yuque/0/2021/png/288075/1620052115014-137b2df3-b478-447e-b14f-37782846bb5d.png)

  ![image.png](https://cdn.nlark.com/yuque/0/2021/png/288075/1620052176778-84e85719-ea66-45bb-b9aa-727199263f3e.png)

  ![image.png](https://cdn.nlark.com/yuque/0/2021/png/288075/1620053060581-c2f3f126-bbaf-434e-aca9-67e497eefaa4.png)

  r 普通寄存器

  sreg 段寄存器

  print-stack 观察栈中的内容 每次显示10个字

  

  ```
  ;从1加到100
  s: ;开始标号
        jmp start
  message db '1+2+3+...+100=' ;字符串等同于db '1','+','2'...
  ;message代表这一行的汇编地址
  ; 传送准备阶段
  ; ds:si 原始数据串的段地址:偏移地址
  ; es:di 目标位置的段地址:偏移地址
  
  start:
        mov ax,0x7c0 ;设置数据段的段基地址
        mov ds,ax
  
        mov ax,0xb800 ;设置附加段的段基地址到显示缓冲区
        mov es,ax
  
        ;显示字符
        mov si,message
        mov di,0
        mov cx,start - message ;循环次数
  showmsg:
        mov al,[si] ;将si对应的数据(字符)传送到al寄存器
        mov [es:di],al ;将al寄存器的值，传送给显存缓冲区
        inc di ;di偏移地址+1向后面移动一个字节
        mov byte [es:di],0x07 ;为字符设置显示属性颜色等数据
        inc di ;di偏移地址+1向后面移动一个字节，为下一个字节空间放置字符做准备
        inc si ;si +1获取下一个字符
        loop showmsg
  
        ;计算1到100的和
        xor ax,ax ;ax用于存储累加结果，异或指令清零
        mov cx,1
  summate:
        add ax,cx
        inc cx
        cmp cx,100 ;cx与100进行比较
        jle summate ;如果小于等于则跳转
  
        ;分解各个数位
        xor cx,cx ;设置栈段基地址
        mov ss,cx
        mov sp,cx
  
        mov bx,10
        xor cx,cx ;记录实际分解了几个数位
  decompo:
        inc cx
        xor dx,dx
        div bx
        add dl,0x30
        push dx ;16位8086处理器中，压栈出栈必须为一个字，不能为一个字节
        cmp ax,0 ;如果商为0，则结束
        jne decompo
  
        ;以下显示各个数位
  shownum:
        pop dx
        mov [es:di],dl
        inc di
        mov byte [es:di],0x07
        inc di
        loop shownum
  
        jmp $
  
  e: ;结束标号
         times 510 - (e - s) db 0
         db 0x55, 0xaa
  ```

  ![image.png](https://cdn.nlark.com/yuque/0/2021/png/288075/1620055728831-0d33ed0f-c6c2-4961-9cb1-e06a8d25e2ac.png)

  