# 运行调试程序加载器和程序

## 首先将代码写入镜像：

1.主引导扇区程序加载器

![image-20210508142415344](./images/image-20210508142415344.png)

![image-20210508142444514](./images/image-20210508142444514.png)

![image-20210508142453417](./images/image-20210508142453417.png)

2.用户程序

![image-20210508142540011](./images/image-20210508142540011.png)

**在代码中规定了，用户程序在物理地址100处开始**

![image-20210508142552641](./images/image-20210508142552641.png)

## 我们用端口来操作硬盘，必须使用IDE接口，即PATA接口。

这个是不正确的：

![image-20210508142002328](./images/image-20210508142002328.png)

这个是正确的：

![image-20210508142032128](./images/image-20210508142032128.png)

运行结果：

![image-20210508142128544](./images/image-20210508142128544.png)

主引导扇区的加载器已经成功运行，并且成功的把控制权交给了用户程序，用户程序也成功的执行了：显示文本hello world

## 代码：

mbrloader.asm

```
         ;代码清单8-1
         ;文件名：c08_mbr.asm
         ;文件说明：硬盘主引导扇区代码（加载程序） 
         ;创建日期：2011-5-5 18:17
         
         app_lba_start equ 100           ;声明常数（用户程序起始逻辑扇区号）
                                         ;常数的声明不会占用汇编地址
                                    
SECTION mbr align=16 vstart=0x7c00                                     

         ;设置堆栈段和栈指针 
         mov ax,0      
         mov ss,ax
         mov sp,ax
         ;初始化后，栈段和代码段是同一个段，段地址都是0，段的长度都是64kb => 64千字节
         
         ;读取用户程序，代码 ，并写入内存
         mov ax,[cs:phy_base] ;将phy_base偏移地址的20位值存放到寄存器中;ax中存放低16位            ;计算用于加载用户程序的逻辑段地址 
         mov dx,[cs:phy_base+0x02] ;dx中存放高16位
         mov bx,16 ;用20位地址，除以16来计算段地址        
         div bx ;做除法来计算，段地址，商在ax中，(16位的段地址)            
         mov ds,ax ;将段地址传送到数据段寄存器ds                       ;令DS和ES指向该段以进行操作
         mov es,ax ;将段地址传送到额外段寄存器es                       
         ;一旦准备好了段，就可以读用户程序，把它加载到，ds或者es所指向的段中。

         ;以下读取程序的起始部分
         ;先读用户程序第一个扇区
         ;该扇区包含了用户程序头部，用户程序头部包含了该程序的大小、入口点、段重定位表。、
         ;通过分析头部就知道，还需要读几个扇区，才能够完全加载用户程序。
         xor di,di
         ;将100变成32位2进制数00000000 00000000 00000000 01100100
         ;高16位放在di中，低16位放在si中，di肯定是0，所以干脆直接di异或为0得了
         ;将100放到si中
         mov si,app_lba_start ;目前我们规定的            ;程序在硬盘上的起始逻辑扇区号 
         xor bx,bx                       ;加载到DS:0x0000处 
         call read_hard_disk_0 ;发起了过程调用
      
         ;以下判断整个程序有多大
         mov dx,[2] ;0x0002将用户程序总长度高16位取出放到dx                      ;曾经把dx写成了ds，花了二十分钟排错 
         mov ax,[0] ;0x0000将用户程序总长度低16位取出放到ax
         mov bx,512                      ;512字节每扇区
         div bx ;程序总长度，除以每个扇区的长度512字节，商在ax中
         cmp dx,0 ;判断余数是否为0
         jnz @1                          ;未除尽，因此结果比实际扇区数少1 
         dec ax                          ;已经读了一个扇区，扇区总数减1 
   @1:
         cmp ax,0                        ;考虑实际长度小于等于512个字节的情况 
         jz direct
         
         ;读取剩余的扇区
         push ds                         ;以下要用到并改变DS寄存器 

         mov cx,ax                       ;循环次数（剩余扇区数）
   @2:
         mov ax,ds
         add ax,0x20                     ;得到下一个以512字节为边界的段地址
         mov ds,ax  
                              
         xor bx,bx                       ;每次读时，偏移地址始终为0x0000 
         inc si                          ;下一个逻辑扇区 
         call read_hard_disk_0
         loop @2                         ;循环读，直到读完整个功能程序 

         pop ds                          ;恢复数据段基址到用户程序头部段 
      
         ;计算入口点代码段基址 
   direct:
         mov dx,[0x08] ;读取header中的入口点，段地址部分
         mov ax,[0x06]
         call calc_segment_base ;根据整个程序的起始物理内存地址，来计算出这个段的逻辑段地址
         mov [0x06],ax                   ;回填修正后的入口点代码段基址 
      
         ;开始处理段重定位表
         mov cx,[0x0a]                   ;需要重定位的项目数量
         mov bx,0x0c                     ;重定位表首地址
          
 realloc:
         mov dx,[bx+0x02]                ;32位地址的高16位 
         mov ax,[bx]
         call calc_segment_base
         mov [bx],ax                     ;回填段的基址
         add bx,4                        ;下一个重定位项（每项占4个字节） 
         loop realloc 
      
         ;访问用户程序头部段，此时在用户程序中0x04-0x06中是逻辑段内偏移地址，0x06-0x08是逻辑段地址，符合间接绝对远转移要求 
         jmp far [0x04] ;间接绝对远转移指令，跳到用户程序内部执行                  ;转移到用户程序  
         ;处理器在加载器中处理的最后一条指令
         ;jmp far 执行时不会影响到除了cs之外的其他寄存器
         ;进入用户代码部分后，ds=es任然指向用户程序头部段的
;-------------------------------------------------------------------------------
;读第一个硬盘控制器的主盘 标号是过程的名字，简称过程名
read_hard_disk_0:                        ;从硬盘读取一个逻辑扇区
                                         ;输入：DI:SI=起始逻辑扇区号
                                         ;      DS:BX=目标缓冲区地址
         ;将用到的寄存器临时压栈
         push ax
         push bx
         push cx
         push dx
      
         mov dx,0x1f2 ;向0x1f2写入要读取的扇区数
         mov al,1
         out dx,al                       ;读取的扇区数
         
         ;向硬盘接口，写入起始逻辑扇区号的低24位
         inc dx                          ;0x1f3
         ;起始的逻辑扇区号低16位在si中，作为参数传递进来
         ;高12位在di中
         mov ax,si
         out dx,al                       ;LBA地址7~0

         inc dx                          ;0x1f4
         mov al,ah
         out dx,al                       ;LBA地址15~8

         inc dx                          ;0x1f5
         mov ax,di
         out dx,al                       ;LBA地址23~16

         inc dx                          ;0x1f6
         mov al,0xe0                     ;LBA28模式，主盘
         ;ah中的低4位是位27-24，高4位是全0
         ;例子：
         ;al 1110 0000
         ;ah 0000 0001
         ;or 1110 0001
         ;结果是取al的高4位(控制参数)和ah的低4位(LBA地址位27-24)  
         or al,ah                        ;LBA地址27~24
         out dx,al

         inc dx                          ;0x1f7
         mov al,0x20                     ;读命令
         out dx,al

  ;等待硬盘控制器准备好
  .waits:
         in al,dx
         and al,0x88
         cmp al,0x08
         jnz .waits                      ;不忙，且硬盘已准备好数据传输 

         mov cx,256                      ;总共要读取的字数
         mov dx,0x1f0
  .readw:
         in ax,dx
         mov [bx],ax ;将数据放到逻辑地址ds:[bx]处
         add bx,2 ;由于读取的是一个字(2个字节)，所以段内偏移地址bx每次循环都要+2(2个字节)
         loop .readw
         
         ;为了保证程序的连续性，回到调用点时，出栈恢复寄存器
         pop dx
         pop cx
         pop bx
         pop ax
         
         ;return指令，使处理器离开过程，从哪里来回哪里去。
         ret

;-------------------------------------------------------------------------------
calc_segment_base:                       ;计算16位段地址
                                         ;输入：DX:AX=32位物理地址
                                         ;返回：AX=16位段基地址 
         ;目的是得到逻辑段地址
         push dx                          
         
         add ax,[cs:phy_base] ;先将低16位进行相加，可能会有产生进位，如果有进位标志寄存器CF为1
         adc dx,[cs:phy_base+0x02] ;带进位的相加指令，除了将两个操作数相加外，还需要加上标志寄存器的标志位
         ;8086无法进行32位的加法运算，寄存器最大只有16位，为此需要分成两个16位的加法
         ;div 16
         shr ax,4 ;右移
         ror dx,4 ;所有位都向右移，最低位复制到进位标志位和最高位
         ;使dx的低12位为全0
         and dx,0xf000
         or ax,dx
         
         pop dx ;恢复dx的原始内容
         
         ret ;此时ax里面是逻辑段地址 逻辑地址 = 逻辑段地址:逻辑段偏移地址

;-------------------------------------------------------------------------------
         phy_base dd 0x10000 ;伪指令dd开辟了一个32位空间(双字)存放这个地址0x10000(20位)             ;用户程序被加载的物理起始地址
         
 times 510-($-$$) db 0
                  db 0x55,0xaa

```

userapp.asm

```
;包含代码段、数据段、栈段的用户程序
;===============================================================================
SECTION header vstart=0                     ;定义用户程序头部段
    ;伪指令dd声明和初始化一个双字，一个32位的数据
    ;16位数据不足以表示65535以上的数值，所以使用双字记录
    program_length  dd program_end          ;程序总长度[0x00]

    ;用户程序入口点
    code_entry      dw start                ;偏移地址[0x04]
                    dd section.code.start ;后续都会被覆盖 ;段地址[0x06]

    realloc_tbl_len dw (segtbl_end-segtbl_begin)/4
                                            ;段重定位表项个数[0x0a]
                                            ;dd双字,每个表项为4个字节

    ;段重定位表
    segtbl_begin:
    code_segment  dd section.code.start ;后续都会被覆盖 ;[0x0c]
    data_segment  dd section.data.start ;后续都会被覆盖 ;[0x10]
    stack_segment   dd section.stack.start ;后续都会被覆盖  ;[0x14]
    segtbl_end:

;===============================================================================
SECTION code align=16 vstart=0         ;定义代码段1（16字节对齐）
  start:
         ;初始化顺序很重要！如果不先初始化stack段，那么无法访问header段，来初始化stack
         ;初始执行时，DS和ES指向用户程序头部段
         mov ax,[stack_segment] ;由于ds此时指向header段，并且header的vstart=0所以stack_segment的汇编地址就等于偏移地址           ;设置到用户程序自己的堆栈
         mov ss,ax
         mov sp,stack_pointer

         mov ax,[data_segment]          ;设置到用户程序自己的数据段
         mov ds,ax ;从此ds不再指向用户程序header，不能用它访问用户程序头部了

         ;用户程序的主要工作是显示字符串，字符串在数据段中
         ;准备访问显存
         mov ax,0xb800
         mov es,ax
         
         ;为了显示字符串，需要从数据段传送到显存，这是在两个段之间进行操作
         ;ds已经指向字符串所在的段
         ;将字符串的首地址message传送到si
         ;[ds:si]
         ;[es:di]
         mov si,message
         mov di,0 ;显存的段内偏移地址0，传送到di
  next:
         ;开始传送字符到显存
         mov al,[si] ;判断是否为空字符串结尾
         cmp al,0
         je exit ;如果是0则结束
         mov byte [es:di],al ;将al中的字符编码传送到es:di逻辑地址处
         mov byte [es:di+1],0x07 ;颜色属性
         inc si ;下一个字符
         add di,2 ;显存内下一个字
         jmp next
  exit:
         jmp $
         
;===============================================================================
SECTION data align=16 vstart=0
message:
        ;末尾0用来标记字符串的结束
        db 'hello world.',0 ;字符串尾部是数字0，也可以认为是空字符，空字符的编码就是0
;===============================================================================
SECTION stack align=16 vstart=0
        ;resw 128
        ;resb 64
        ;db 256
        ;与下面作用相同
        resb 256 ;保留若干字节reserved bytes，并不初始化它们的值
        ;跳过这些字节，这些字节中的内容，不确定
stack_pointer: ;因为vstart=0，所以stack_pointer的汇编地址就是stack段内偏移地址
;可以直接用来作为栈顶指针

;===============================================================================
SECTION trail align=16
;没有vstart语句，
;所以标号program_end的汇编地址是从整个程序的开头计算的
;program_end长度为整个程序的长度
program_end:

```

