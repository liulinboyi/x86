# 安装0x70中断的处理过程

通过程序学习如何使用中断。

我们的程序分为两个部分，一个是加载器，一个是用户程序。

加载器：

```
         ;代码清单8-1
         ;文件名：c08_mbr.asm
         ;文件说明：硬盘主引导扇区代码（加载程序） 
         ;创建日期：2011-5-5 18:17
         
         app_lba_start equ 100           ;声明常数（用户程序起始逻辑扇区号）
                                         ;常数的声明不会占用汇编地址
                                    
SECTION mbr align=16 vstart=0x7c00                                     

         ;设置堆栈段和栈指针 
         mov ax,0      
         mov ss,ax
         mov sp,ax
         ;初始化后，栈段和代码段是同一个段，段地址都是0，段的长度都是64kb => 64千字节
         
         ;读取用户程序，代码 ，并写入内存
         mov ax,[cs:phy_base] ;将phy_base偏移地址的20位值存放到寄存器中;ax中存放低16位            ;计算用于加载用户程序的逻辑段地址 
         mov dx,[cs:phy_base+0x02] ;dx中存放高16位
         mov bx,16 ;用20位地址，除以16来计算段地址        
         div bx ;做除法来计算，段地址，商在ax中，(16位的段地址)            
         mov ds,ax ;将段地址传送到数据段寄存器ds                       ;令DS和ES指向该段以进行操作
         mov es,ax ;将段地址传送到额外段寄存器es                       
         ;一旦准备好了段，就可以读用户程序，把它加载到，ds或者es所指向的段中。

         ;以下读取程序的起始部分
         ;先读用户程序第一个扇区
         ;该扇区包含了用户程序头部，用户程序头部包含了该程序的大小、入口点、段重定位表。、
         ;通过分析头部就知道，还需要读几个扇区，才能够完全加载用户程序。
         xor di,di
         ;将100变成32位2进制数00000000 00000000 00000000 01100100
         ;高16位放在di中，低16位放在si中，di肯定是0，所以干脆直接di异或为0得了
         ;将100放到si中
         mov si,app_lba_start ;目前我们规定的            ;程序在硬盘上的起始逻辑扇区号 
         xor bx,bx                       ;加载到DS:0x0000处 
         call read_hard_disk_0 ;发起了过程调用
      
         ;以下判断整个程序有多大
         mov dx,[2] ;0x0002将用户程序总长度高16位取出放到dx                      ;曾经把dx写成了ds，花了二十分钟排错 
         mov ax,[0] ;0x0000将用户程序总长度低16位取出放到ax
         mov bx,512                      ;512字节每扇区
         div bx ;程序总长度，除以每个扇区的长度512字节，商在ax中
         cmp dx,0 ;判断余数是否为0
         jnz @1                          ;未除尽，因此结果比实际扇区数少1 
         dec ax                          ;已经读了一个扇区，扇区总数减1 
   @1:
         cmp ax,0                        ;考虑实际长度小于等于512个字节的情况 
         jz direct
         
         ;读取剩余的扇区
         push ds                         ;以下要用到并改变DS寄存器 

         mov cx,ax                       ;循环次数（剩余扇区数）
   @2:
         mov ax,ds
         add ax,0x20                     ;得到下一个以512字节为边界的段地址
         mov ds,ax  
                              
         xor bx,bx                       ;每次读时，偏移地址始终为0x0000 
         inc si                          ;下一个逻辑扇区 
         call read_hard_disk_0
         loop @2                         ;循环读，直到读完整个功能程序 

         pop ds                          ;恢复数据段基址到用户程序头部段 
      
         ;计算入口点代码段基址 
   direct:
         mov dx,[0x08] ;读取header中的入口点，段地址部分
         mov ax,[0x06]
         call calc_segment_base ;根据整个程序的起始物理内存地址，来计算出这个段的逻辑段地址
         mov [0x06],ax                   ;回填修正后的入口点代码段基址 
      
         ;开始处理段重定位表
         mov cx,[0x0a]                   ;需要重定位的项目数量
         mov bx,0x0c                     ;重定位表首地址
          
 realloc:
         mov dx,[bx+0x02]                ;32位地址的高16位 
         mov ax,[bx]
         call calc_segment_base
         mov [bx],ax                     ;回填段的基址
         add bx,4                        ;下一个重定位项（每项占4个字节） 
         loop realloc 
      
         ;访问用户程序头部段，此时在用户程序中0x04-0x06中是逻辑段内偏移地址，0x06-0x08是逻辑段地址，符合间接绝对远转移要求 
         jmp far [0x04] ;跳到用户程序entry(程序入口点)处，间接绝对远转移指令，跳到用户程序内部执行                  ;转移到用户程序  
         ;处理器在加载器中处理的最后一条指令
         ;jmp far 执行时不会影响到除了cs之外的其他寄存器
         ;进入用户代码部分后，ds=es任然指向用户程序头部段的
;-------------------------------------------------------------------------------
;读第一个硬盘控制器的主盘 标号是过程的名字，简称过程名
read_hard_disk_0:                        ;从硬盘读取一个逻辑扇区
                                         ;输入：DI:SI=起始逻辑扇区号
                                         ;      DS:BX=目标缓冲区地址
         ;将用到的寄存器临时压栈
         push ax
         push bx
         push cx
         push dx
      
         mov dx,0x1f2 ;向0x1f2写入要读取的扇区数
         mov al,1
         out dx,al                       ;读取的扇区数
         
         ;向硬盘接口，写入起始逻辑扇区号的低24位
         inc dx                          ;0x1f3
         ;起始的逻辑扇区号低16位在si中，作为参数传递进来
         ;高12位在di中
         mov ax,si
         out dx,al                       ;LBA地址7~0

         inc dx                          ;0x1f4
         mov al,ah
         out dx,al                       ;LBA地址15~8

         inc dx                          ;0x1f5
         mov ax,di
         out dx,al                       ;LBA地址23~16

         inc dx                          ;0x1f6
         mov al,0xe0                     ;LBA28模式，主盘
         ;ah中的低4位是位27-24，高4位是全0
         ;例子：
         ;al 1110 0000
         ;ah 0000 0001
         ;or 1110 0001
         ;结果是取al的高4位(控制参数)和ah的低4位(LBA地址位27-24)  
         or al,ah                        ;LBA地址27~24
         out dx,al

         inc dx                          ;0x1f7
         mov al,0x20                     ;读命令
         out dx,al

  ;等待硬盘控制器准备好
  .waits:
         in al,dx
         and al,0x88
         cmp al,0x08
         jnz .waits                      ;不忙，且硬盘已准备好数据传输 

         mov cx,256                      ;总共要读取的字数
         mov dx,0x1f0
  .readw:
         in ax,dx
         mov [bx],ax ;将数据放到逻辑地址ds:[bx]处
         add bx,2 ;由于读取的是一个字(2个字节)，所以段内偏移地址bx每次循环都要+2(2个字节)
         loop .readw
         
         ;为了保证程序的连续性，回到调用点时，出栈恢复寄存器
         pop dx
         pop cx
         pop bx
         pop ax
         
         ;return指令，使处理器离开过程，从哪里来回哪里去。
         ret

;-------------------------------------------------------------------------------
calc_segment_base:                       ;计算16位段地址
                                         ;输入：DX:AX=32位物理地址
                                         ;返回：AX=16位段基地址 
         ;目的是得到逻辑段地址
         push dx                          
         
         add ax,[cs:phy_base] ;先将低16位进行相加，可能会有产生进位，如果有进位标志寄存器CF为1
         adc dx,[cs:phy_base+0x02] ;带进位的相加指令，除了将两个操作数相加外，还需要加上标志寄存器的标志位
         ;8086无法进行32位的加法运算，寄存器最大只有16位，为此需要分成两个16位的加法
         ;div 16
         shr ax,4 ;右移
         ror dx,4 ;所有位都向右移，最低位复制到进位标志位和最高位
         ;使dx的低12位为全0
         and dx,0xf000
         or ax,dx
         
         pop dx ;恢复dx的原始内容
         
         ret ;此时ax里面是逻辑段地址 逻辑地址 = 逻辑段地址:逻辑段偏移地址

;-------------------------------------------------------------------------------
         phy_base dd 0x10000 ;伪指令dd开辟了一个32位空间(双字)存放这个地址0x10000(20位)             ;用户程序被加载的物理起始地址
         
 times 510-($-$$) db 0
                  db 0x55,0xaa

```

**当处理器执行任何一条改变栈段寄存器ss的指令时，它会在下一条指令执行完期间禁止中断。**

```
;**当处理器执行任何一条改变栈段寄存器ss的指令时，它会在下一条指令执行完期间禁止中断。**
mov ss,ax ;将栈段的，段基地址传送给ss 处理器发现修改的是栈段寄存器ss，所以从现在开始-
;-一直到下一条指令mov sp,ss_pointer执行完成之前，处理器禁止任何中断，不会处理任何中断。
mov sp,ss_pointer ;传送栈顶地址到sp
```

原因：

栈是十分重要的，要想改变代码段或者数据段，直接改变段寄存器就可以了，但是栈段就不同，因为除了栈寄存器ss，还有栈指针sp，绝大多数时候，我们对栈的改变，是分两步进行的，先修改栈寄存器的内容，接着再修改栈指针的内容。

如果刚刚修改了栈寄存器ss，在还没有来得及修改栈指针sp的情况下，就发生了中断，会出现什么后果呢？

中断是依靠栈来工作的，当出现中断时，要压入cs和ip的内容，然后转去执行中断处理过程，将来还要依靠从栈中弹出这些内容来返回，如果只是修改了ss，还没来得及修改sp，就发生了中断，这些信息会压入哪里呢？肯定是压入到了，无法预料的地方，而且将来中断返回也成了问题，因此处理器在设计时就规定，**当处理器执行任何一条改变栈段寄存器ss的指令时，它会在下一条指令执行完期间禁止中断。** **也就是说，如果你要修改ss，那么后面要紧跟一条修改sp的指令，不能分开。**修改sp的指令必须在修改ss的指令之后。

我们的任务时演示中断处理，而且要用我们自己的实时时钟中断处理过程，来取代系统开机后默认的中断处理过程，下面在中断向量表中安装实时时钟中断的入口地址，在安装向量之前，应该显示一些信息，这样比较友好，

当计算机启动之后，分配给RTC芯片的中断号默认是0x70，尽管我们可以通过8259A编程来修改中断号，但是没有必要，我们使用默认的中断号0x70就行，这样是为了简单。

为了修改某个中断在中断表中的登记项，需要先找到它。

```
         ;代码清单9-1
         ;文件名：c09_1.asm
         ;文件说明：用户程序 
         ;创建日期：2011-4-16 22:03
         
;===============================================================================
SECTION header vstart=0                     ;定义用户程序头部段 
    program_length  dd program_end          ;程序总长度[0x00]
    
    ;用户程序入口点
    code_entry      dw start                ;偏移地址[0x04]
                    dd section.code.start   ;段地址[0x06] 
    
    realloc_tbl_len dw (header_end-realloc_begin)/4
                                            ;段重定位表项个数[0x0a]
    
    realloc_begin:
    ;段重定位表           
    code_segment    dd section.code.start   ;[0x0c]
    data_segment    dd section.data.start   ;[0x14]
    stack_segment   dd section.stack.start  ;[0x1c]
    
header_end:                
    
;===============================================================================
SECTION code align=16 vstart=0           ;定义代码段（16字节对齐） 
new_int_0x70: ;新的中断处理过程 - 实时时钟中断处理过程
      push ax
      push bx
      push cx
      push dx
      push es
      
  .w0:                                    
      mov al,0x0a                        ;阻断NMI。当然，通常是不必要的
      or al,0x80                          
      out 0x70,al
      in al,0x71                         ;读寄存器A
      test al,0x80                       ;测试第7位UIP 
      jnz .w0                            ;以上代码对于更新周期结束中断来说 
                                         ;是不必要的 
      xor al,al
      or al,0x80
      out 0x70,al
      in al,0x71                         ;读RTC当前时间(秒)
      push ax

      mov al,2
      or al,0x80
      out 0x70,al
      in al,0x71                         ;读RTC当前时间(分)
      push ax

      mov al,4
      or al,0x80
      out 0x70,al
      in al,0x71                         ;读RTC当前时间(时)
      push ax

      mov al,0x0c                        ;寄存器C的索引。且开放NMI 
      out 0x70,al
      in al,0x71                         ;读一下RTC的寄存器C，否则只发生一次中断
                                         ;此处不考虑闹钟和周期性中断的情况 
      mov ax,0xb800
      mov es,ax

      pop ax
      call bcd_to_ascii
      mov bx,12*160 + 36*2               ;从屏幕上的12行36列开始显示

      mov [es:bx],ah
      mov [es:bx+2],al                   ;显示两位小时数字

      mov al,':'
      mov [es:bx+4],al                   ;显示分隔符':'
      not byte [es:bx+5]                 ;反转显示属性 

      pop ax
      call bcd_to_ascii
      mov [es:bx+6],ah
      mov [es:bx+8],al                   ;显示两位分钟数字

      mov al,':'
      mov [es:bx+10],al                  ;显示分隔符':'
      not byte [es:bx+11]                ;反转显示属性

      pop ax
      call bcd_to_ascii
      mov [es:bx+12],ah
      mov [es:bx+14],al                  ;显示两位小时数字
      
      mov al,0x20                        ;中断结束命令EOI 
      out 0xa0,al                        ;向从片发送 
      out 0x20,al                        ;向主片发送 

      pop es
      pop dx
      pop cx
      pop bx
      pop ax

      iret

;-------------------------------------------------------------------------------
bcd_to_ascii:                            ;BCD码转ASCII
                                         ;输入：AL=bcd码
                                         ;输出：AX=ascii
      mov ah,al                          ;分拆成两个数字 
      and al,0x0f                        ;仅保留低4位 
      add al,0x30                        ;转换成ASCII 

      shr ah,4                           ;逻辑右移4位 
      and ah,0x0f                        
      add ah,0x30

      ret

;-------------------------------------------------------------------------------
start: ;初始化栈段和数据段
      mov ax,[stack_segment] ;将栈段的，段基地址传送给ax
      ;**当处理器执行任何一条改变栈段寄存器ss的指令时，它会在下一条指令执行完期间禁止中断。**
      mov ss,ax ;将栈段的，段基地址传送给ss 处理器发现修改的是栈段寄存器ss，所以从现在开始-
      ;-一直到下一条指令mov sp,ss_pointer执行完成之前，处理器禁止任何中断，不会处理任何中断。
      mov sp,ss_pointer ;传送栈顶地址到sp
      mov ax,[data_segment] ;将数据段的，段基地址传送给ax
      mov ds,ax ;将数据段的，段基地址传送给ds
      
      ;打印提示信息
      mov bx,init_msg                    ;显示初始信息 
      call put_string

      mov bx,inst_msg                    ;显示安装信息 
      call put_string
      
      mov al,0x70 ;RTC实时时钟中断号
      mov bl,4 ;将中断号乘以4，乘积就是0x70号中断登记项的物理地址
      mul bl ;被乘数al和乘数bl相乘，结果在ax里                             ;计算0x70号中断在IVT中的偏移
      mov bx,ax ;因为后面会用到ax，所有将值存到bx中                          

      ;修改中断向量表时，要先用cli清中断，0x70号中断的入口信息，
      ;需要好几条指令才能修改完成，
      ;在此期间，如果入口地址刚改了一半，就发生了0x70号中断，
      ;这个时候就会转到错误的地方去执行，后果就严重了。
      ;所以我们必须先关中断，
      cli ;将标志寄存器的中断标志IF清0，以禁止处理器响应中断                                ;防止改动期间发生新的0x70号中断

      ;接下来访问中断向量表所在的段，然后修改0x70中断所对应的表项，
      push es
      mov ax,0x0000
      mov es,ax ;将es修改为中断向量表所在的段地址，这个段中所有物理地址就是段内偏移地址
      mov word [es:bx],new_int_0x70      ;偏移地址。
      ;将中断向量表中的实时时钟中断处理过程换成code:new_int_0x70处                                    
      mov word [es:bx+2],cs              ;段地址
      pop es

      mov al,0x0b                        ;RTC寄存器B
      or al,0x80                         ;阻断NMI 
      out 0x70,al
      mov al,0x12                        ;设置寄存器B，禁止周期性中断，开放更 
      out 0x71,al                        ;新结束后中断，BCD码，24小时制 

      mov al,0x0c
      out 0x70,al
      in al,0x71                         ;读RTC寄存器C，复位未决的中断状态

      in al,0xa1                         ;读8259从片的IMR寄存器 
      and al,0xfe                        ;清除bit 0(此位连接RTC)
      out 0xa1,al                        ;写回此寄存器 

      sti                                ;重新开放中断 

      mov bx,done_msg                    ;显示安装完成信息 
      call put_string

      mov bx,tips_msg                    ;显示提示信息
      call put_string
      
      mov cx,0xb800
      mov ds,cx
      mov byte [12*160 + 33*2],'@'       ;屏幕第12行，35列
       
 .idle:
      hlt                                ;使CPU进入低功耗状态，直到用中断唤醒
      not byte [12*160 + 33*2+1]         ;反转显示属性 
      jmp .idle

;-------------------------------------------------------------------------------
put_string:                              ;显示串(0结尾)。
                                         ;输入：DS:BX=串地址
         mov cl,[bx]
         or cl,cl                        ;cl=0 ?
         jz .exit                        ;是的，返回主程序 
         call put_char
         inc bx                          ;下一个字符 
         jmp put_string

   .exit:
         ret

;-------------------------------------------------------------------------------
put_char:                                ;显示一个字符
                                         ;输入：cl=字符ascii
         push ax
         push bx
         push cx
         push dx
         push ds
         push es

         ;以下取当前光标位置
         mov dx,0x3d4
         mov al,0x0e
         out dx,al
         mov dx,0x3d5
         in al,dx                        ;高8位 
         mov ah,al

         mov dx,0x3d4
         mov al,0x0f
         out dx,al
         mov dx,0x3d5
         in al,dx                        ;低8位 
         mov bx,ax                       ;BX=代表光标位置的16位数

         cmp cl,0x0d                     ;回车符？
         jnz .put_0a                     ;不是。看看是不是换行等字符 
         mov ax,bx                       ; 
         mov bl,80                       
         div bl
         mul bl
         mov bx,ax
         jmp .set_cursor

 .put_0a:
         cmp cl,0x0a                     ;换行符？
         jnz .put_other                  ;不是，那就正常显示字符 
         add bx,80
         jmp .roll_screen

 .put_other:                             ;正常显示字符
         mov ax,0xb800
         mov es,ax
         shl bx,1
         mov [es:bx],cl

         ;以下将光标位置推进一个字符
         shr bx,1
         add bx,1

 .roll_screen:
         cmp bx,2000                     ;光标超出屏幕？滚屏
         jl .set_cursor

         mov ax,0xb800
         mov ds,ax
         mov es,ax
         cld
         mov si,0xa0
         mov di,0x00
         mov cx,1920
         rep movsw
         mov bx,3840                     ;清除屏幕最底一行
         mov cx,80
 .cls:
         mov word[es:bx],0x0720
         add bx,2
         loop .cls

         mov bx,1920

 .set_cursor:
         mov dx,0x3d4
         mov al,0x0e
         out dx,al
         mov dx,0x3d5
         mov al,bh
         out dx,al
         mov dx,0x3d4
         mov al,0x0f
         out dx,al
         mov dx,0x3d5
         mov al,bl
         out dx,al

         pop es
         pop ds
         pop dx
         pop cx
         pop bx
         pop ax

         ret

;===============================================================================
SECTION data align=16 vstart=0

    init_msg       db 'Starting...',0x0d,0x0a,0
                   
    inst_msg       db 'Installing a new interrupt 70H...',0
    
    done_msg       db 'Done.',0x0d,0x0a,0

    tips_msg       db 'Clock is now working.',0
                   
;===============================================================================
SECTION stack align=16 vstart=0
           
                 resb 256
ss_pointer:
 
;===============================================================================
SECTION program_trail
program_end:
```

