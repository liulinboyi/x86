# 如何在程序中访问另外一个不同的数据段

逻辑地址：形如 => 逻辑段地址:逻辑段内偏移地址，作用是计算得到20位物理地址，计算过程：

将逻辑段地址左移4位，加上逻辑段内偏移地址，得到20位的物理地址

例子：0xb800:0x0001 => 0xb800左移4位 => 0xb8000 => 结果加上0x0001 => 0xb8000 + 0x0001 => 0xb8001 => 得到20位物理地址(**一个16进制数位表示4个二机制数位**)

逻辑段地址：形如0xb800

逻辑段内偏移地址：形如0x0001

```
         ;代码清单8-2
         ;文件名：c08.asm
         ;文件说明：用户程序 
         ;创建日期：2011-5-5 18:17
         
;===============================================================================
SECTION header vstart=0                     ;定义用户程序头部段 
    program_length  dd program_end          ;程序总长度[0x00]
    
    ;用户程序入口点
    code_entry      dw start                ;偏移地址[0x04]
                    dd section.code_1.start ;段地址[0x06] 
    
    realloc_tbl_len dw (header_end-code_1_segment)/4
                                            ;段重定位表项个数[0x0a]
    
    ;段重定位表           
    code_1_segment  dd section.code_1.start ;[0x0c]
    code_2_segment  dd section.code_2.start ;[0x10]
    data_1_segment  dd section.data_1.start ;[0x14]
    data_2_segment  dd section.data_2.start ;[0x18]
    stack_segment   dd section.stack.start  ;[0x1c]
    
    header_end:                
    
;===============================================================================
SECTION code_1 align=16 vstart=0         ;定义代码段1（16字节对齐） 
put_string:                              ;显示串(0结尾)。
                                         ;输入：DS:BX=串地址
         mov cl,[bx]
         or cl,cl ;cmp cl,0                        ;cl=0 ?
         jz .exit ;条件转移指令，依赖标志位                        ;是的，返回主程序 
         call put_char
         inc bx                          ;下一个字符 
         jmp put_string

   .exit:
         ret

;-------------------------------------------------------------------------------
put_char:                                ;显示一个字符
                                         ;输入：cl=字符ascii
         push ax
         push bx
         push cx
         push dx
         push ds
         push es

         ;以下取当前光标位置
         mov dx,0x3d4 ;将显卡索引端口存入dx
         mov al,0x0e ;将光标的内部索引al
         out dx,al ;将光标的内部索引，传送到索引端口
         mov dx,0x3d5 ;通过数据端口获取光标的一个8位值(一个字节的数据)
         in al,dx ;读出的数据在al中                        ;高8位 
         mov ah,al ;将al的数据传送到ah中

         mov dx,0x3d4
         mov al,0x0f
         out dx,al
         mov dx,0x3d5
         in al,dx                        ;低8位 
         mov bx,ax ;ax中是完整的光标位置数据，将这个数值存入到bx中临时保存                       ;BX=代表光标位置的16位数

         cmp cl,0x0d                     ;回车符？
         jnz .put_0a                     ;不是。看看是不是换行等字符 
         mov ax,bx                       ;此句略显多余，但去掉后还得改书，麻烦 
         mov bl,80                     
         div bl ;用寄存器ax的光标位置，除以bl中的80，在al中得到当前行的行号  
         mul bl ;将寄存器al中的内容乘以bl中的80会在寄存器ax中得到当前行，行首的光标数值
         mov bx,ax ;将值传送到bx中保存
         jmp .set_cursor

 .put_0a:
         cmp cl,0x0a                     ;换行符？
         jnz .put_other                  ;不是，那就正常显示字符 
         add bx,80
         jmp .roll_screen

 .put_other:                             ;正常显示字符
         mov ax,0xb800
         mov es,ax
         shl bx,1 ;显存中一个字符占两个字节，所以根据光标位置计算字符在显存内偏移地址
         mov [es:bx],cl ;在光标处写入字符

         ;在光标处写入字符后，必须让光标推进一格，指示下一个字符即将出现的位置
         ;以下将光标位置推进一个字符
         shr bx,1
         add bx,1

 .roll_screen:
         cmp bx,2000                     ;光标超出屏幕？滚屏
         jl .set_cursor
         
         push bx
         mov ax,0xb800
         mov ds,ax
         mov es,ax
         cld ;清方向标志，表明是正向传送
         mov si,0xa0
         mov di,0x00
         mov cx,1920
         rep movsw ;批量传送
         
         ;向屏幕最后一行写入80个空格
         mov bx,3840 ;最后一行在显存中的偏移地址是3840 = 24 * 80 * 2 24行每行80个字，一个字在显存中占两个字节                     ;清除屏幕最底一行
         mov cx,80
 .cls:
         mov word[es:bx],0x0720 ;空格
         add bx,2
         loop .cls

         ;mov bx,1920
         pop bx
         sub bx,80
;设置光标在屏幕上的位置
 .set_cursor:
         mov dx,0x3d4
         mov al,0x0e
         out dx,al
         mov dx,0x3d5
         mov al,bh
         out dx,al
         mov dx,0x3d4
         mov al,0x0f
         out dx,al
         mov dx,0x3d5
         mov al,bl
         out dx,al

         pop es
         pop ds
         pop dx
         pop cx
         pop bx
         pop ax

         ret

;-------------------------------------------------------------------------------
  start:
         ;初始执行时，DS和ES指向用户程序头部段
         mov ax,[stack_segment]           ;设置到用户程序自己的堆栈 
         mov ss,ax
         mov sp,stack_end
         
         mov ax,[data_1_segment]          ;设置到用户程序自己的数据段
         mov ds,ax

         mov bx,msg0
         call put_string                  ;显示第一段信息 

         push word [es:code_2_segment]
         mov ax,begin
         push ax                          ;可以直接push begin,80386+
         
         retf ;假装从过程返回，从而进入到另一个代码段中执行                             ;转移到代码段2执行 
         
  continue:
         mov ax,[es:data_2_segment]       ;段寄存器DS切换到数据段2 
         mov ds,ax ;换了一个数据段
         
         mov bx,msg1
         call put_string                  ;显示第二段信息 

         jmp $ 

;===============================================================================
SECTION code_2 align=16 vstart=0          ;定义代码段2（16字节对齐）

  begin:
         push word [es:code_1_segment] ;先push段地址
         mov ax,continue
         push ax ;再push段内偏移地址                          ;可以直接push continue,80386+
         ;在栈中是这样的形式存储的

         ;高位
         ;段内偏移地址
         ;段地址
         ;低位
         

         retf ;假装从过程返回，从而进入到另一个代码段中执行                             ;转移到代码段1接着执行 
         
;===============================================================================
SECTION data_1 align=16 vstart=0

    msg0 db '  This is NASM - the famous Netwide Assembler. '
         db 'Back at SourceForge and in intensive development! '
         db 'Get the current versions from http://www.nasm.us/.'
         db 0x0d,0x0a,0x0d,0x0a
         db '  Example code for calculate 1+2+...+1000:',0x0d,0x0a,0x0d,0x0a
         db '     xor dx,dx',0x0d,0x0a
         db '     xor ax,ax',0x0d,0x0a
         db '     xor cx,cx',0x0d,0x0a
         db '  @@:',0x0d,0x0a
         db '     inc cx',0x0d,0x0a
         db '     add ax,cx',0x0d,0x0a
         db '     adc dx,0',0x0d,0x0a
         db '     inc cx',0x0d,0x0a
         db '     cmp cx,1000',0x0d,0x0a
         db '     jle @@',0x0d,0x0a
         db '     ... ...(Some other codes)',0x0d,0x0a,0x0d,0x0a
         db 0

;===============================================================================
SECTION data_2 align=16 vstart=0
    ;因为有vstart=0，所以msg1的汇编地址就是段内偏移地址
    msg1 db '  The above contents is written by LeeChung. '
         db '2011-05-06'
         db 0

;===============================================================================
SECTION stack align=16 vstart=0
           
         resb 256

stack_end:  

;===============================================================================
SECTION trail align=16
program_end:
```



通常用户程序执行完之后，应当重新将控制返回给加载器，加载器可以重新运行和加载其他程序，所有操作系统都是这么做的，目前我们的加载器没有这样的功能，而且用户程序也没有将控制返回给加载器。

